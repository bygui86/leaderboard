import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.1.0'
        classpath "io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE"
    }
}
plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '1.5.6.RELEASE'
}

apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'io.spring.dependency-management'


dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-sleuth:1.2.5.RELEASE'
    }
}

repositories {
    jcenter()
    maven {
        url "https://grpcvsrest.s3.amazonaws.com/snapshots"
    }
}

dependencies {
    compile project(':grpc-api')
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.cloud:spring-cloud-sleuth-zipkin'
    compile 'org.springframework.cloud:spring-cloud-starter-sleuth'
    compile 'com.google.guava:guava:23.0'
    compile 'io.grpc:grpc-all:1.6.1'
    compile 'io.zipkin.brave:brave-instrumentation-grpc:4.9.1'
    compile 'io.zipkin.reporter2:zipkin-sender-urlconnection:2.1.3'

    testCompile 'junit:junit:4.12'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.assertj:assertj-core:3.5.+'
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'xmlunit:xmlunit:1.6'
}

idea.module {
    outputDir = file('idea-build')
    testOutputDir = file('idea-test-build')
}

bootRepackage {
    mainClass = 'org.grpcvsrest.leaderboard.Application'
}

task buildImage(type: DockerBuildImage) {
    dependsOn bootRepackage
    inputDir = project.projectDir
    tag = 'leaderboard'
}


task buildDocker(type: DockerBuildImage) {
    dependsOn bootRepackage
    inputDir = project.projectDir
    tag = 'leaderboard:latest'
}

task tagDocker(type: Exec) {
    dependsOn buildDocker
    commandLine 'docker', 'tag', 'leaderboard:latest', 'gcr.io/alien-fold-180922/leaderboard:latest'
}

task pushDocker(type: Exec) {
    dependsOn tagDocker
    commandLine 'gcloud', 'docker', '--', 'push', 'gcr.io/alien-fold-180922/leaderboard:latest'
}

task deploy(type: Exec) {
    commandLine './k8s/deploy.sh'
}
